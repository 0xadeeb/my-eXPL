type
    bst{
        int a;
        bst left;
        bst right;
    }
endtype
class
    bstclass{
        decl
            bst root;
            int init();
            bst getroot();
            int setroot(bst n1);
            bst getnode(int key);
            bst insert(bst h, int key);
            int inOrder_fun(bst h);
            int preOrder_fun(bst h);
            int postOrder_fun(bst h);
        enddecl
        int init(){
            begin
                self.root=null;
            end
                return 1;
        }
        bst getroot(){
            begin
            end
                return self.root;
        }
        int setroot(bst n1){
            begin
                self.root=n1;
            end
                return 1;
        }
        bst getnode(int key){
            decl
                bst temp;
            enddecl
            begin
                temp=alloc();
                temp.a=key;
                temp.left=null;
                temp.right=null;
            end
                return temp;
        }
        bst insert(bst h, int key){
            begin
                if (h == null) then
                    h = self.getnode(key);
                else
                    if (key < h.a) then
                        h.left = self.insert(h.left, key);
                    else
                        if (key > h.a) then
                            h.right = self.insert(h.right, key);
                        endif;
                    endif;
                endif;
            end
                return h;
        }
        int inOrder_fun(bst h){
            decl
                int in;
            enddecl
            begin
                if(h!= null) then
                    in=self.inOrder_fun(h.left);
                    write(h.a);
                    in=self.inOrder_fun(h.right);
                endif;
            end
                return 1;
        }
        int preOrder_fun(bst h){
            decl
                int in;
            enddecl
            begin
                if(h!= null) then
                    write(h.a);
                    in=self.preOrder_fun(h.left);
                    in=self.preOrder_fun(h.right);
                endif;
            end
                return 1;
        }
        int postOrder_fun(bst h){
            decl
                int in;
            enddecl
            begin
                if(h!= null) then
                    in=self.postOrder_fun(h.left);
                    in=self.postOrder_fun(h.right);
                    write(h.a);
                endif;
            end
                return 1;
        }
    }
endclass
decl
    bstclass obj;
enddecl
int main(){
    decl
        bst Root;
        int x,in,val;
    enddecl
    begin
        initialize();
        obj = new(bstclass);
        x=obj.init();
        read(val);
        Root = obj.getroot();
        while(val!=0) do
            Root = obj.insert(Root,val);
            read(val);
        endwhile;
        x = obj.setroot(Root);
        write("InOrder");
        in = obj.inOrder_fun(obj.getroot());
        write("PreOrder");
        in = obj.preOrder_fun(obj.getroot());
        write("PostOrder");
        in = obj.postOrder_fun(obj.getroot());
    end
        return 0;
}

